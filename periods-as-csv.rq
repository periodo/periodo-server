prefix dc: <http://purl.org/dc/terms/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix periodo: <http://n2t.net/ark:/99152/p0v#>
prefix prov: <http://www.w3.org/ns/prov#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix time: <http://www.w3.org/2006/time#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT
(?concept as ?period)
(group_concat(distinct ?labels;separator='|') as ?label)
(group_concat(distinct ?places;separator=' | ') as ?spatial_coverage)
(group_concat(distinct ?place_uri;separator=' ') as ?gazetteer_links)
(xsd:integer(group_concat(distinct ?starts;separator='|')) as ?start)
(group_concat(distinct ?stops;separator='|') as ?stop)
(group_concat(distinct ?scheme;separator='|') as ?authority)
(group_concat(distinct ?sources;separator=' | ') as ?source)
(group_concat(distinct ?issued;separator='|') as ?publication_year)
(group_concat(distinct ?derived;separator=' ') as ?derived_periods)
(group_concat(distinct ?broader;separator=' ') as ?broader_periods)
(group_concat(distinct ?narrower;separator=' ') as ?narrower_periods)

WHERE {
  ?concept a skos:Concept ;
    skos:prefLabel ?labels ;
    skos:inScheme ?scheme
  .
  OPTIONAL {
    ?scheme
    dc:source/dc:isPartOf? [
      dc:title|((dc:creator|dc:contributor)/foaf:name) ?sources
    ]
    .
  }
  OPTIONAL {
    ?scheme
    dc:source/dc:isPartOf? [
      dc:issued ?issued
    ]
    .
  }
  OPTIONAL {
    ?concept
    periodo:spatialCoverageDescription|(dc:spatial/skos:prefLabel)
    ?places
    .
  }
  OPTIONAL {
    ?concept dc:spatial ?place_uri .
  }
  OPTIONAL {
    ?concept
    time:intervalStartedBy/time:hasDateTimeDescription [
      (time:year|periodo:earliestYear) ?starts
    ]
    .
  }
  OPTIONAL {
    ?concept
    time:intervalFinishedBy/time:hasDateTimeDescription [
      (time:year|periodo:latestYear) ?stops
    ]
    .
  }
  OPTIONAL {
    ?derived prov:wasDerivedFrom ?concept .
  }
  OPTIONAL {
    ?concept skos:broader ?broader .
  }
  OPTIONAL {
    ?narrower skos:broader ?concept .
  }
}
GROUP BY ?concept
ORDER BY ?publication_year ?source ?spatial_coverage ?start
